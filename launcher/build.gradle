/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

archivesBaseName = 'jruyi-launcher'
version = jruyi_launcher_version
description = 'The executable to run JRuyi as a standalone application'

ext {
	title = 'JRuyi Launcher'
}

dependencies {
	compile "org.osgi:org.osgi.core:$org_osgi_core_version"
	compile "commons-cli:commons-cli:$commons_cli_version"

	compile project(':api')
}

jar {
	manifest {
		attributes(
				'Specification-Title': title,
				'Specification-Version': version,
				'Specification-Vendor': organizationName,
				'Specification-Vendor-Id': organizationId,
				'Implementation-Title': title,
				'Implementation-Version': version,
				'Implementation-Vendor': organizationName,
				'Created-By': createdBy,
				'Main-Class': 'org.jruyi.launcher.Main',
		)
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {

			artifactId archivesBaseName

			from components.java

			pom.withXml {
				def projectNode = asNode()
				projectNode.appendNode('name', title)
				projectNode.appendNode('description', description)
				projectNode.appendNode('url', organizationUrl)

				def organizationNode = projectNode.appendNode('organization')
				organizationNode.appendNode('name', organizationName)
				organizationNode.appendNode('url', organizationUrl)

				def licenseNode = projectNode.appendNode('licenses').appendNode('license')
				licenseNode.appendNode('name', licenseName)
				licenseNode.appendNode('url', licenseUrl)
				licenseNode.appendNode('distribution', 'repo')

				projectNode.dependencies.'*'.findAll() {
					it.scope.text() == 'runtime'
				}.each() {
					it.scope*.value = 'provided'
				}
			}

			artifact sourcesJar
		}
	}
}

bintray {
	pkg {
		name = archivesBaseName
		desc = description
		version {
			name = project.version
		}
	}
}
