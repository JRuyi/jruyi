/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'org.jruyi.osgibnd'

archivesBaseName = 'jruyi-core'
version = jruyi_core_version
description = 'The core of JRuyi, a Java framework for easily developing highly efficient and highly scalable network applications'

ext {
	title = 'JRuyi Core'
}

dependencies {
	compile "org.slf4j:slf4j-api:1.7.14"
	compile "com.lmax:disruptor:3.3.4"

	compile "org.jruyi:jruyi-api:2.5.0"

	compile project(':common')
	compile project(':io')
}

processResources {
	configurations.compile.files.each { jar ->
		if (jar.name.startsWith('org.jruyi.common') || jar.name.startsWith('org.jruyi.io')) {
			from(zipTree(jar)) {
				exclude '**/META-INF/**', '**/OSGI-INF/**', '**/HELP-INF/**'
			}
		}
	}
}

jar {
	manifest {
		attributes(
				'Export-Package': 'org.jruyi.common,org.jruyi.io,org.jruyi.core',
				'Import-Package': '!org.osgi.*,*',
		)
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {

			artifactId archivesBaseName

			from components.java

			pom.withXml {
				def node = asNode()
				node.children().last() + {
					resolveStrategy = Closure.DELEGATE_FIRST
					name title
					description description
					url websiteUrl
					inceptionYear '2015'
					scm {
						url websiteUrl
						connection "scm:$vcsUrl"
						developerConnection "scm:$vcsUrl"
					}
					licenses {
						license {
							name licenseName
							url licenseUrl
							distribution 'repo'
						}
					}
					developers {
						developer {
							id developerId
							name developerName
							email developerEmail
						}
					}
				}

				node.dependencies.'*'.findAll() {
					it.scope.text() == 'runtime'
				}.each() {
					it.scope*.value = 'provided'
				}
			}

			artifact sourcesJar
		}
	}
}

bintray {
	pkg {
		name = archivesBaseName
		desc = description
		version {
			name = project.version
		}
	}
}
