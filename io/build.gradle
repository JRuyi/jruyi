/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'groovy'
apply plugin: 'org.jruyi.osgibnd'

archivesBaseName = 'org.jruyi.io'
version = org_jruyi_io_version
description = 'Provides an event based asynchronous IO framework'

ext {
	title = 'JRuyi IO'
}

dependencies {
	compile "com.lmax:disruptor:3.3.4"
	compile "org.osgi:org.osgi.core:4.3.1"
	compile "org.osgi:org.osgi.compendium:4.3.1"
	compile "org.apache.felix:org.apache.felix.gogo.runtime:0.16.2"
	compile "org.slf4j:slf4j-api:1.7.12"

	compile "org.jruyi:jruyi-api:2.3.1"

	testCompile "org.codehaus.groovy:groovy-all:$groovy_version"
	testCompile "org.spockframework:spock-core:$spock_core_version"
	testCompile "org.slf4j:slf4j-simple:$slf4j_version"
	testCompile "org.objenesis:objenesis:$objenesis_version"
	testCompile "cglib:cglib-nodep:$cglib_nodep_version"
	testCompile "org.hamcrest:hamcrest-core:$hamcrest_version"
	testCompile "junit:junit:$junit_version"

	testCompile project(':common')
}

compileJava {
	options.compilerArgs << '-XDignore.symbol.file'
	options.fork = true
	options.forkOptions.executable = javaCompilerExecutable
}

jar {
	manifest {
		attributes(
				'Export-Package': 'org.jruyi.io',
				'-dsannotations': '*',
				'Bundle-Activator': 'org.jruyi.io.internal.Activator',
		)
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {

			artifactId archivesBaseName

			from components.java

			pom.withXml {
				def projectNode = asNode()
				projectNode.appendNode('name', title)
				projectNode.appendNode('description', description)
				projectNode.appendNode('url', organizationUrl)

				def organizationNode = projectNode.appendNode('organization')
				organizationNode.appendNode('name', organizationName)
				organizationNode.appendNode('url', organizationUrl)

				def licenseNode = projectNode.appendNode('licenses').appendNode('license')
				licenseNode.appendNode('name', licenseName)
				licenseNode.appendNode('url', licenseUrl)
				licenseNode.appendNode('distribution', 'repo')

				projectNode.dependencies.'*'.findAll() {
					it.scope.text() == 'runtime'
				}.each() {
					it.scope*.value = 'provided'
				}
			}

			artifact sourcesJar
		}
	}
}

bintray {
	pkg {
		name = archivesBaseName
		desc = description
		version {
			name = project.version
		}
	}
}
