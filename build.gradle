/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
	repositories {
		maven {
			url 'http://repo.jruyi.org/maven/releases'
		}
	}

	dependencies {
		classpath 'org.jruyi:osgibnd-gradle-plugin:0.1.0'
	}
}

apply plugin: 'distribution'

version = '1.3.1'
description = 'A Java framework for easily developing efficient and scalable network applications'

ext {
	title = 'JRuyi'
}

allprojects {

	group = 'org.jruyi'

	ext {
		organizationId = 'org.jruyi'
		organizationName = 'JRuyi.org'
		organizationUrl = 'http://www.jruyi.org/'

		licenseName = 'Apache License, Version 2.0'
		licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'

		createdBy = "${System.getProperty('java.version')} (${System.getProperty('java.vm.vendor')})"
	}

	ext {
		org_osgi_core_version = '5.0.0'
		org_osgi_compendium_version = '5.0.0'
		commons_cli_version = '1.2'
		jline_version = '2.12'
		logback_core_version = '1.1.2'
		logback_classic_version = '1.1.2'
		org_apache_felix_bundlerepository_version = '2.0.2'
		org_apache_felix_configadmin_version = '1.8.0'
		org_apache_felix_framework_version = '4.4.1'
		org_apache_felix_gogo_runtime_version = '0.12.1'
		org_apache_felix_metatype_version = '1.0.10'
		org_apache_felix_scr_version = '1.8.2'
		slf4j_api_version = '1.7.7'

		jruyi_system_version = rootProject.version

		jruyi_api_version = '1.3.1'
		jruyi_cli_version = '1.0.5'
		jruyi_launcher_version = '1.0.5'
		org_jruyi_clid_version = '1.1.3'
		org_jruyi_cmd_version = '1.1.4'
		org_jruyi_common_version = '1.3.1'
		org_jruyi_io_version = '1.2.4'
		org_jruyi_me_version = '1.1.4'
		org_jruyi_osgi_log_version = '1.0.3'
		org_jruyi_timeoutadmin_version = '1.1.3'
		org_jruyi_workshop_version = '1.1.4'
	}

	repositories {
		mavenCentral()
	}
}

subprojects {

	apply plugin: 'java'
	apply plugin: 'maven'

	configurations.compile {
		transitive = false
	}

	compileJava {
		sourceCompatibility = 1.6
		targetCompatibility = 1.6
		options.encoding = 'UTF-8'
	}

	compileTestJava {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
		options.encoding = 'UTF-8'
	}

	processResources {
		from(["$rootDir/LICENSE", 'NOTICE']) {
			into 'META-INF'
		}
	}

	uploadArchives {
		repositories {
			mavenDeployer {
				repository(url: jruyiRepo)
				pom {
					name = title
				}
				pom.project {
					url = organizationUrl
					description = project.description
					organization {
						name = organizationName
						url = organizationUrl
					}
					licenses {
						license {
							name licenseName
							url licenseUrl
							distribution 'repo'
						}
					}
				}
			}
		}
	}
}

configurations {
	main
	lib
	bundle
}

configurations.all {
	transitive = false
}

dependencies {
	main project(':launcher')
	main project(':cli')

	lib "commons-cli:commons-cli:$commons_cli_version"
	lib "jline:jline:$jline_version"
	lib "org.slf4j:slf4j-api:$slf4j_api_version"
	lib "ch.qos.logback:logback-core:$logback_core_version"
	lib "ch.qos.logback:logback-classic:$logback_classic_version"

	lib project(':system')

	bundle "org.apache.felix:org.apache.felix.configadmin:$org_apache_felix_configadmin_version"
	bundle "org.apache.felix:org.apache.felix.framework:$org_apache_felix_framework_version"
	bundle "org.apache.felix:org.apache.felix.gogo.runtime:$org_apache_felix_gogo_runtime_version"
	bundle "org.apache.felix:org.apache.felix.metatype:$org_apache_felix_metatype_version"
	bundle "org.apache.felix:org.apache.felix.scr:$org_apache_felix_scr_version"

	bundle project(':clid')
	bundle project(':cmd')
	bundle project(':common')
	bundle project(':io')
	bundle project(':me')
	bundle project(':osgi-log')
	bundle project(':timeoutadmin')
	bundle project(':workshop')
}

distributions {
	main {
		contents {
			from(['LICENSE', 'NOTICE'])

			from('src/main/bin') {
				include '**/*.bat'
				into 'bin'
				expand([
						jruyiLauncherVersion: jruyi_launcher_version,
						jruyiCliVersion     : jruyi_cli_version,
				])
			}
			from('src/main/bin') {
				exclude '**/*.bat'
				fileMode = 0755
				into 'bin'
				expand([
						jruyiLauncherVersion: jruyi_launcher_version,
						jruyiCliVersion     : jruyi_cli_version,
				])
			}

			from('src/main/conf') {
				include 'bootstrap.xml'
				into 'inst/default/conf'
				expand([
						felixFrameworkVersion   : org_apache_felix_framework_version,
						felixMetatypeVersion    : org_apache_felix_metatype_version,
						felixConfigAdminVersion : org_apache_felix_configadmin_version,
						felixScrVersion         : org_apache_felix_scr_version,
						felixGogoRuntimeVersion : org_apache_felix_gogo_runtime_version,

						jruyiOsgiLogVersion     : org_jruyi_osgi_log_version,
						jruyiCommonVersion      : org_jruyi_common_version,
						jruyiWorkshopVersion    : org_jruyi_workshop_version,
						jruyiTimeoutAdminVersion: org_jruyi_timeoutadmin_version,
						jruyiMeVersion          : org_jruyi_me_version,
						jruyiIoVersion          : org_jruyi_io_version,
						jruyiCmdVersion         : org_jruyi_cmd_version,
						jruyiClidVersion        : org_jruyi_clid_version,
				])
			}

			from('src/main/conf') {
				include 'logback.xml'
				into 'inst/default/conf'
			}

			from(configurations.main) {
				into 'main'
			}

			from(configurations.lib) {
				into 'lib'
			}

			from(configurations.bundle) {
				into 'bundles'
			}
		}
	}
}

distTar {
	compression = Compression.GZIP
	extension = 'tar.gz'
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.0'
}
