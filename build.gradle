/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
	repositories {
		maven {
			url 'https://plugins.gradle.org/m2/'
		}
	}

	dependencies {
		classpath 'gradle.plugin.org.jruyi.gradle:osgibnd-gradle-plugin:0.5.0'
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.5'
	}
}

apply plugin: 'distribution'

version = '2.5.0-SNAPSHOT'
description = 'A Java framework for easily developing highly efficient and highly scalable network applications'

ext {
	title = 'JRuyi'
}

allprojects {

	group = 'org.jruyi'

	ext {
		organizationId = 'org.jruyi'
		organizationName = 'JRuyi.org'
		organizationUrl = 'http://www.jruyi.org/'

		licenseName = 'Apache License, Version 2.0'
		licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'

		createdBy = "${System.getProperty('java.version')} (${System.getProperty('java.vm.vendor')})"

		javaCompilerExecutable = System.env['JAVA_HOME'] ? System.env['JAVA_HOME'] + '/bin/javac' : 'javac'
	}

	ext {
		commons_cli_version = '1.3.1'
		disruptor_version = '3.3.4'
		jline_version = '2.13'
		log4j_version = '2.5'
		org_apache_felix_configadmin_version = '1.8.8'
		org_apache_felix_framework_version = '5.4.0'
		org_apache_felix_gogo_runtime_version = '0.16.2'
		org_apache_felix_metatype_version = '1.1.2'
		org_apache_felix_scr_version = '2.0.2'
		slf4j_version = '1.7.13'

		cglib_nodep_version = '3.1'
		groovy_version = '2.4.5'
		hamcrest_version = '1.3'
		junit_version = '4.12'
		objenesis_version = '2.2'
		spock_core_version = '1.0-groovy-2.4'

		jruyi_system_version = rootProject.version

		jruyi_core_version = '2.5.0-SNAPSHOT'

		jruyi_api_version = '2.5.0-SNAPSHOT'
		jruyi_cli_version = '2.0.2-SNAPSHOT'
		jruyi_launcher_version = '2.0.2-SNAPSHOT'
		org_jruyi_clid_version = '2.3.3'
		org_jruyi_cmd_version = '2.0.4'
		org_jruyi_common_version = '2.4.0'
		org_jruyi_io_version = '2.3.5-SNAPSHOT'
		org_jruyi_osgi_log_version = '2.0.2'
		org_jruyi_tpe_version = '2.0.2'
	}

	repositories {
		jcenter()
	}

	configurations.all {
		transitive = false
	}
}

subprojects {

	apply plugin: 'java'
	apply plugin: 'maven-publish'
	apply plugin: 'com.jfrog.bintray'

	compileJava {
		sourceCompatibility = 1.7
		targetCompatibility = 1.7
		options.encoding = 'UTF-8'
	}

	compileTestJava {
		sourceCompatibility = 1.7
		targetCompatibility = 1.7
		options.encoding = 'UTF-8'
	}

	processResources {
		from(["$rootDir/LICENSE", 'NOTICE']) {
			into 'META-INF'
		}
	}

	task sourcesJar(type: Jar, dependsOn: classes) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

	bintray {
		user = bintrayUser
		key = bintrayApiKey
		publications = ['mavenJava']
		publish = true
		pkg {
			repo = 'maven'
			userOrg = 'jruyi'
			name = archivesBaseName
			desc = description
			websiteUrl = 'https://github.com/jruyi/jruyi'
			issueTrackerUrl = 'https://github.com/jruyi/jruyi/issues'
			vcsUrl = 'https://github.com/jruyi/jruyi.git'
			licenses = ['Apache-2.0']
			labels = ['jruyi', project.name]
			publicDownloadNumbers = true
			version {
				name = project.version
				vcsTag = "v${rootProject.version}"
			}
		}
	}
}

configurations {
	main
	lib
	bundle
}

dependencies {
	main project(':launcher')
	main project(':cli')

	lib "commons-cli:commons-cli:$commons_cli_version"
	lib "com.lmax:disruptor:$disruptor_version"
	lib "jline:jline:$jline_version"
	lib "org.slf4j:slf4j-api:$slf4j_version"
	lib "org.apache.logging.log4j:log4j-api:$log4j_version"
	lib "org.apache.logging.log4j:log4j-core:$log4j_version"
	lib "org.apache.logging.log4j:log4j-slf4j-impl:$log4j_version"

	lib project(':system')

	bundle "org.apache.felix:org.apache.felix.configadmin:$org_apache_felix_configadmin_version"
	bundle "org.apache.felix:org.apache.felix.framework:$org_apache_felix_framework_version"
	bundle "org.apache.felix:org.apache.felix.gogo.runtime:$org_apache_felix_gogo_runtime_version"
	bundle "org.apache.felix:org.apache.felix.metatype:$org_apache_felix_metatype_version"
	bundle "org.apache.felix:org.apache.felix.scr:$org_apache_felix_scr_version"

	bundle project(':clid')
	bundle project(':cmd')
	bundle project(':common')
	bundle project(':io')
	bundle project(':osgi-log')
	bundle project(':tpe')
}

distributions {
	main {
		contents {
			from(['LICENSE', 'NOTICE'])

			from('src/main/bin') {
				include '**/*.bat'
				into 'bin'
				expand([
						jruyiLauncherVersion: jruyi_launcher_version,
						jruyiCliVersion     : jruyi_cli_version,
				])
			}
			from('src/main/bin') {
				exclude '**/*.bat'
				fileMode = 0755
				into 'bin'
				expand([
						jruyiLauncherVersion: jruyi_launcher_version,
						jruyiCliVersion     : jruyi_cli_version,
				])
			}

			from('src/main/conf') {
				include 'bootstrap.xml'
				into 'inst/default/conf'
				expand([
						felixConfigAdminVersion : org_apache_felix_configadmin_version,
						felixFrameworkVersion   : org_apache_felix_framework_version,
						felixGogoRuntimeVersion : org_apache_felix_gogo_runtime_version,
						felixMetatypeVersion    : org_apache_felix_metatype_version,
						felixScrVersion         : org_apache_felix_scr_version,

						jruyiCmdVersion         : org_jruyi_cmd_version,
						jruyiClidVersion        : org_jruyi_clid_version,
						jruyiCommonVersion      : org_jruyi_common_version,
						jruyiIoVersion          : org_jruyi_io_version,
						jruyiOsgiLogVersion     : org_jruyi_osgi_log_version,
						jruyiTpeVersion         : org_jruyi_tpe_version,
				])
			}

			from('src/main/conf') {
				exclude 'bootstrap.xml'
				into 'inst/default/conf'
			}

			from(configurations.main) {
				into 'main'
			}

			from(configurations.lib) {
				into 'lib'
			}

			from(configurations.bundle) {
				into 'bundles'
			}
		}
	}
}

distTar {
	compression = Compression.GZIP
	extension = 'tar.gz'
}
