/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'org.jruyi.osgibnd'

archivesBaseName = 'org.jruyi.osgi.log'
version = org_jruyi_osgi_log_version
description = 'A simple partial implementation of OSGi log service'

ext {
	title = 'JRuyi OSGi Log Service'
}

dependencies {
	compile "org.osgi:org.osgi.core:4.3.1"
	compile "org.osgi:org.osgi.compendium:4.3.1"
	compile "org.slf4j:slf4j-api:1.7.14"

	compile "org.jruyi:jruyi-api:2.0.0"
}

jar {
	manifest {
		attributes(
				'Export-Package': 'org.osgi.service.log',
				'Bundle-Activator': 'org.jruyi.osgi.log.Activator',
		)
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {

			artifactId archivesBaseName

			from components.java

			pom.withXml {
				def node = asNode()
				node.children().last() + {
					resolveStrategy = Closure.DELEGATE_FIRST
					name title
					description description
					url websiteUrl
					inceptionYear '2011'
					scm {
						url websiteUrl
						connection "scm:$vcsUrl"
						developerConnection "scm:$vcsUrl"
					}
					licenses {
						license {
							name licenseName
							url licenseUrl
							distribution 'repo'
						}
					}
					developers {
						developer {
							id developerId
							name developerName
							email developerEmail
						}
					}
				}

				node.dependencies.'*'.findAll() {
					it.scope.text() == 'runtime'
				}.each() {
					it.scope*.value = 'provided'
				}
			}

			artifact sourcesJar
		}
	}
}

bintray {
	pkg {
		name = archivesBaseName
		desc = description
		version {
			name = project.version
		}
	}
}
